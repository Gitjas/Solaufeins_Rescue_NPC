BACKUP ~C#Solaufein/backup~
AUTHOR ~Please post at G3 or Kerzenburgforum, refer to readme.~ 


VERSION ~4.1~ 

README ~C#Solaufein/readme.solaufeinsrescue.%LANGUAGE%.html~ ~C#Solaufein/readme.solaufeinsrescue.english.html~

AUTO_TRA ~C#Solaufein/Translations/autotra/%s~


ALWAYS



  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END



//CamDawgs CD_STATE_NOTVALID zum Checken der Dialogfähigkeit eines Char
//Thank you very much, CamDawg!
APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
 ~0x80101FEF CD_STATE_NOTVALID~
 UNLESS ~CD_STATE_NOTVALID~


    OUTER_SET SOLAUFEIN_TIMER = 1200
    OUTER_SET SOLAUFEIN_TIMERTOB = 900
    OUTER_SET SOLAUFEIN_BANTER_FAST = 1800
    OUTER_SET SOLAUFEIN_BANTER_NORMAL = 3600
    OUTER_SET SOLAUFEIN_BANTER_SLOW = 5400

    OUTER_SPRINT ~Drideranimation_male~ ~DRIDER_MALE~
    OUTER_SPRINT ~Drideranimation_female~ ~DRIDER_FEMALE~

  ACTION_IF GAME_IS ~eet~ THEN BEGIN
    OUTER_SPRINT ~IsContiGameToB~ ~Global("NEWGAME_TOB","GLOBAL",0)~
    OUTER_SPRINT ~IsNewGameToB~ ~Global("NEWGAME_TOB","GLOBAL",1)~
  END
  ACTION_IF NOT GAME_IS ~eet~ THEN BEGIN
    OUTER_SPRINT ~IsContiGameToB~ ~False()~
    OUTER_SPRINT ~IsNewGameToB~ ~~
  END

/* extras needed for 7th party member mode */
  OUTER_SPRINT "NPCname" "Solaufein"
  ACTION_IF GAME_IS ~eet bg2ee~ THEN BEGIN
    OUTER_SPRINT ~RunAwayFromNoLeaveArea~ ~RunAwayFromNoLeaveArea~
  END ELSE BEGIN
    OUTER_SPRINT ~RunAwayFromNoLeaveArea~ ~RunAwayFrom~
  END

/* all following actions are only processed ONCE for the whole mod, independent of un- and reinstalling of single components */

ACTION_IF !FILE_EXISTS ~C#Solaufein/Install/c#solaufeinreinstall.mrk~ BEGIN

  /*
   * Copies tra files into the autotra-folder (to leave the originals untouched)
   */
  DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
    COPY ~C#Solaufein/Translations/%LANGUAGE%~  ~C#Solaufein/Translations/autotra/%LANGUAGE%~
  END

  LAF autotra_workaround END


ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END

ACTION_DEFINE_ARRAY fl#reload BEGIN game END

   LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~C#Solaufein/Translations/autotra~
      noconvert_array = fl#noconvert
      reload_array = fl#reload
  END



//////////////////////////////////////////////////////////////////////////////////
////Give WILSON banter file as used in the Wilson chronicles ///
//////////////////////////////////////////////////////////////////////////////////


ACTION_IF FILE_EXISTS_IN_GAME ~wilson.dlg~ THEN BEGIN
 PRINT ~Wilson detected - Wilson interdia.2da append - set Wilsons's banter file (BWILSON & BWILSO25)~
    APPEND ~interdia.2da~ ~WILSON BWILSON BWILSO25~
    UNLESS ~WILSON~
   ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BWILSON.dlg~) THEN BEGIN
      <<<<<<<< ...inlined/BWILSON.d
      BEGIN BWILSON
      >>>>>>>>
      COMPILE ~...inlined/BWILSON.d~
  END
   ACTION_IF NOT (FILE_EXISTS_IN_GAME ~BWILSO25.dlg~) THEN BEGIN
      <<<<<<<< ...inlined/BWILSO25.d
      BEGIN BWILSO25
      >>>>>>>>
      COMPILE ~...inlined/BWILSO25.d~
  END
END


COPY ~C#Solaufein/install/component.xx~ ~C#Solaufein/Install/c#solaufeinreinstall.mrk~

END //c#solaufeinreinstall.mrk


    //TRAs declared in LANGUAGE must be reloaded
  LOAD_TRA ~C#Solaufein/Translations/autotra/english/game.tra~
  LOAD_TRA ~C#Solaufein/Translations/autotra/%LANGUAGE%/game.tra~


END //ALWAYS


LANGUAGE ~Deutsch~
         ~german~   
         ~C#Solaufein/Translations/german/setup.tra~
         ~C#Solaufein/Translations/german/game.tra~

LANGUAGE ~English~
         ~english~   
         ~C#Solaufein/Translations/english/setup.tra~
         ~C#Solaufein/Translations/english/game.tra~

LANGUAGE ~Russian~
         ~russian~   
         ~C#Solaufein/Translations/english/setup.tra~
         ~C#Solaufein/Translations/russian/game.tra~

LANGUAGE ~Italian~
         ~italian~   
         ~C#Solaufein/Translations/italian/setup.tra~
         ~C#Solaufein/Translations/italian/game.tra~


BEGIN @90000
DESIGNATED 0
LABEL JasteysSolaufeinRescue-Main
REQUIRE_PREDICATE GAME_IS ~bgt bg2ee tob eet~ @90011
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @90012
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @90012


ADD_JOURNAL TITLE (@1799) @67 @1795 @1796 @1797 @1798 USING ~C#Solaufein/Translations/autotra/%LANGUAGE%/C#SOLATRA.TRA~


COPY_EXISTING ~ar2100.are~ ~override~
  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 18
    fj_box_top      = 2509
    fj_box_right    = 103
    fj_box_bottom   = 2689
    fj_cursor_idx   = 30   //door
    fj_vertex_0     = 103 + (2509 << 16)
    fj_vertex_1     = 90 + (2509 << 16)
    fj_vertex_2     = 90 + (2689 << 16)
    fj_vertex_3     = 203 + (2689 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = Tranc#so
    fj_destination_area = c#so01
    fj_destination_name = Exit2100
  END


  LAUNCH_PATCH_FUNCTION  ~fj_are_structure~
    INT_VAR
    fj_loc_x       = 147
    fj_loc_y       = 2634
    fj_orientation = 12   //E
    STR_VAR
    fj_structure_type = entrance
    fj_name           = Exitc#so
  END



//trigger point with script
  LPF ~fj_are_structure~
    INT_VAR
    fj_type         = 1    //trigger
    fj_box_left     = 18
    fj_box_top      = 2509
    fj_box_right    = 103
    fj_box_bottom   = 2689
    fj_cursor_idx   = 30   //door
    fj_vertex_0     = 103 + (2509 << 16)
    fj_vertex_1     = 90 + (2509 << 16)
    fj_vertex_2     = 90 + (2689 << 16)
    fj_vertex_3     = 203 + (2689 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#sorune
    fj_reg_script   = c#sopass
END




//container and item
  LPF ~fj_are_structure~
    INT_VAR
    fj_type        = 8 //nonvisible
    fj_loc_x       = 3810
    fj_loc_y       = 1962
    fj_box_left    = 3809
    fj_box_top     = 1783
    fj_box_right   = 3824
    fj_box_bottom  = 1826
    fj_trap_loc_x  = 3810
    fj_trap_loc_y  = 1962
    fj_vertex_0    = 3809 + (1880 << 16)
    fj_vertex_1    = 3818 + (1823 << 16)
    fj_vertex_2    = 3824 + (1826 << 16)
    fj_vertex_3    = 3819 + (1783 << 16)
    STR_VAR
    fj_structure_type = container
    fj_name           = ~C#Felsspalte~
  END

BUT_ONLY




/* areas */
COPY ~C#Solaufein/area~ ~Override~

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
  COPY ~C#Solaufein/area/C#SO01_EE.MOS~ ~Override/C#SO01.MOS~
  COPY ~C#Solaufein/pvrz~ ~Override~
END
ELSE BEGIN
  COPY ~C#Solaufein/tis_v1~ ~Override~
END

/* place ambient into override folder to prevent area from crashing */
COPY_EXISTING ~AM0804.WAV~ ~override~


/* bams */
COPY ~C#Solaufein/bams~ ~Override~


/* vvc */
COPY ~C#Solaufein/vvc~ ~Override~



/* effects */
COPY ~C#Solaufein/eff~ ~Override~


/* items */

COPY ~C#Solaufein/items/c#sorun1.itm~ ~override/c#sorun1.itm~
SAY NAME1 @1
SAY NAME2 @1
SAY UNIDENTIFIED_DESC @1
SAY DESC  @2

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
COPY_EXISTING  ~c#sorun1.itm~ ~override~
WRITE_ASCII 0x58 ~CMISC2N~ #8
END


COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/C#SOLAU.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/C#SOLAUJ.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/c#solaufein_interjections.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/c#solaufein_interjections_ee.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
END

COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/scenery_dialogues_soa.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/sola_quest.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/C#SOLAUB.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

EXTEND_BOTTOM ~mazzyd.bcs~ ~C#Solaufein/scripts/c#solau_mazzyd.baf~ EVALUATE_BUFFER

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/C#SOLAUB_ee.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
END


/* ------------  tob ------------------ - */


ACTION_IF GAME_IS ~tob bg2ee eet~ THEN BEGIN 

  COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/C#SOLAU_ToB.d~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

  //  epilogue: ##
  EXTEND_TOP ~AR6200.bcs~ ~c#solaufein/scripts/solau_epilog_AR6200.baf~

  /* no friendship */
  COPY ~c#solaufein/2da/c#soepxx.2da~ ~override/c#soep01.2da~
  REPLACE ~99999~ @3

  /* friendship */
  COPY ~c#solaufein/2da/c#soepxx.2da~ ~override/c#soep02.2da~
  REPLACE ~99999~ @4

END

/* -------------------------------------- */


//The PID, to be compiled as last dialogue file
 COMPILE ~C#Solaufein/Dialogues/C#SOLAU_PID.d~ 
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 


COMPILE EVALUATE_BUFFER ~C#Solaufein/Scripts/C#SOLAU.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~

ACTION_IF GAME_IS ~bgt tob~ THEN BEGIN
  EXTEND_BOTTOM ~C#SOLAU.bcs~   ~C#Solaufein/Scripts/c#_lvl.baf~ EVALUATE_BUFFER
END

/* extras needed for 7th party member mode */
EXTEND_BOTTOM ~C#SOLAU.bcs~ ~%MOD_FOLDER%/7thpmm/script_patch/set_7pmm_scripts.baf~ EVALUATE_BUFFER

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  EXTEND_BOTTOM ~C#SOLAU.bcs~   ~C#Solaufein/Scripts/c#_lvl_ee.baf~ EVALUATE_BUFFER
  EXTEND_BOTTOM ~C#SOLAU.bcs~   ~C#Solaufein/Scripts/C#solau_ee.baf~ EVALUATE_BUFFER
END


COMPILE ~C#Solaufein/Scripts/C#SOLAU1.baf~
COMPILE ~C#Solaufein/Scripts/c#socut1.baf~
COMPILE ~C#Solaufein/Scripts/c#socut2.baf~
COMPILE ~C#Solaufein/Scripts/c#socut3.baf~


COMPILE ~C#Solaufein/Scripts/c#sodrsc.baf~
COMPILE ~C#Solaufein/Scripts/c#sodrf0.baf~
COMPILE ~C#Solaufein/Scripts/c#sodrm1.baf~

COMPILE ~C#Solaufein/Scripts/c#somob1.baf~
COMPILE ~C#Solaufein/Scripts/c#somob5.baf~

COMPILE ~C#Solaufein/Scripts/c#so01.baf~

COMPILE ~C#Solaufein/Scripts/c#sopass.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE ~C#Solaufein/Scripts/c#sococ1.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE ~C#Solaufein/Scripts/c#sococ2.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE ~C#Solaufein/Scripts/c#sococ3.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE ~C#Solaufein/Scripts/c#sococ4.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE ~C#Solaufein/Scripts/c#sococ5.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE ~C#Solaufein/Scripts/c#sococ6.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
COMPILE ~C#Solaufein/Scripts/c#sobrut.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

EXTEND_BOTTOM ~AR0300.bcs~ ~C#Solaufein/Scripts/C#Solau_AR0300.baf~
EXTEND_BOTTOM ~AR0800.bcs~ ~C#Solaufein/Scripts/C#Solau_AR0800.baf~
EXTEND_BOTTOM ~AR0900.bcs~ ~C#Solaufein/Scripts/C#Solau_AR0900.baf~
EXTEND_BOTTOM ~AR1000.bcs~ ~C#Solaufein/Scripts/C#Solau_AR1000.baf~

EXTEND_TOP ~AR2100.bcs~ ~C#Solaufein/Scripts/C#Solau_AR2100.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 

EXTEND_TOP ~AR2401.bcs~ ~C#Solaufein/Scripts/C#Solau_AR2401.baf~

EXTEND_TOP ~AR2500.bcs~ ~C#Solaufein/Scripts/C#Solau_AR2500.baf~

EXTEND_BOTTOM ~AMCARRAS.bcs~ ~C#Solaufein/Scripts/AMCARRAS_add.baf~



/* extras needed for 7th party member mode */
INCLUDE ~%MOD_FOLDER%/7thpmm/c#7thpmm_thingies.tpa~

/* Solaufein kit */

INCLUDE ~C#Solaufein/lib/fl#add_kit_ee.tpa~

ADD_KIT ~C#SoDrow~ ///Solaufein's drow kit
  // appended to CLASWEAP.2da
  ~C#SoDrow                 1           1           1           1           1           1           1           1
~

  // appended column-wise to WEAPPROF.2da
//  ~C#SoDrow  5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 2 2 2 3 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5~
  ~C#SoDrow  5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 5 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
     
  // appended to ABCLASRQ.2da
  ~C#SoDrow                 9       0       0       0       0       0
~

  // appended to ABCLSMOD.2da
  ~C#SoDrow                 0       0       0       0       0       0~

  // appended to ABDCDSRQ.2da
  ~C#SoDrow                 17      0       0       0       0       0
~

  // appended to ABDCSCRQ.2da
  ~C#SoDrow                 15      0       0       0       0       0~

  // appended to ALIGNMNT.2da
  ~C#SoDrow                 1       1       1       1       1       1       1       1       1~

  // appended to DUALCLAS.2da
  ~C#SoDrow                 0       0       0       0       0       0~

  // path to your CLAB-style 2da file 
  ~C#Solaufein/2da/C#SoDrow.2da~

  // PC races and classes allowed to use this kit, from KITTABLE.2da
  ~~

  // usability flags, added to the end of the KITLIST.2da entry
  ~0x00004000    2~       ///vanilla fighter

  // HLA table to use; see LUABBR.2da
  ~Fi0~

  // list of starting equipment for PCs starting in ToB. Appended column-wise to 25STWEAP.2da
  ~* * * * * * * * * * * * * * * * * * * *~

  // Lower, mixed, and help kit names and descriptions, added to KITLIST.2da 
  SAY @5
  SAY @5
  SAY @6


LAF fl#add_kit_ee STR_VAR kit_name = C#SoDrow END


/*  spells */


COPY ~C#Solaufein/spells/c#soblin.spl~ ~override/c#soblin.spl~
COPY ~C#Solaufein/spells/c#sobrec.spl~ ~override/c#sobrec.spl~
COPY ~C#Solaufein/spells/c#somar2.spl~ ~override/c#somar2.spl~
COPY ~C#Solaufein/spells/c#somar3.spl~ ~override/c#somar3.spl~
COPY ~C#Solaufein/spells/c#somar4.spl~ ~override/c#somar4.spl~
COPY ~C#Solaufein/spells/c#somar5.spl~ ~override/c#somar5.spl~


COPY ~C#Solaufein/spells/c#sofeen.spl~ ~override/c#sofeen.spl~
  SAY NAME1 @7 
SAY UNIDENTIFIED_DESC  @38 
SAY DESC  @38 

COPY ~C#Solaufein/spells/c#sodark.spl~ ~override/c#sodark.spl~
  SAY NAME1 @8
SAY UNIDENTIFIED_DESC  @37 
SAY DESC  @37 

COPY ~C#Solaufein/spells/c#sodimd.spl~ ~override/c#sodimd.spl~
  SAY NAME1 @9


/* Portraits */

ACTION_IF GAME_IS ~bg2 tob bgt~ BEGIN

COPY ~C#Solaufein/Portraits/Portrait1/C#SolauL.bmp~ ~override/C#SolauL.bmp~
COPY ~C#Solaufein/Portraits/Portrait1/C#SolauM.bmp~ ~override/C#SolauM.bmp~
COPY ~C#Solaufein/Portraits/Portrait1/C#SolauS.bmp~ ~override/C#SolauS.bmp~

END

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN

COPY ~C#Solaufein/Portraits/Portrait1/C#SolauL.bmp~ ~override/C#SolauL.bmp~
COPY ~C#Solaufein/Portraits/Portrait1/Portrait1_EE/C#SolauM.bmp~ ~override/C#SolauM.bmp~
COPY ~C#Solaufein/Portraits/Portrait1/Portrait1_EE/C#SolauS.bmp~ ~override/C#SolauS.bmp~

END



/* sound */


COPY ~C#Solaufein/Creatures/C#SOlau1.cre~ ~override/C#SOlau1.CRE~ 


ACTION_IF (FILE_EXISTS ~C#Solaufein/sound/%LANGUAGE%~) BEGIN
  COPY ~C#Solaufein/sound/%LANGUAGE%~ ~override~
END
ELSE BEGIN
  COPY ~C#Solaufein/sound/english~ ~override~
END

/* mute sound, for PID and dialogues */
COPY ~C#Solaufein/sound/c#ablank.wav~ ~override~


/* cre */

COPY 	~C#Solaufein/Creatures/C#Sola12.cre~ ~override/C#Sola12.cre~
 	~C#Solaufein/Creatures/C#Soltob.cre~ ~override/C#Soltob.cre~
  WRITE_SHORT   0x244 0
  WRITE_SHORT 0x246 0x4000 + ~%C#SoDrow%~
SAY NAME1 #50874 //~Solaufein~
SAY NAME2 #50874 //~Solaufein~
  SAY INITIAL_MEETING @12
  SAY MORALE @13
  SAY HAPPY @14
  SAY UNHAPPY_ANNOYED @15
  SAY UNHAPPY_SERIOUS @16
  SAY UNHAPPY_BREAKING @16
  SAY LEADER @17
  SAY TIRED @18
  SAY BORED @19 
  SAY BATTLE_CRY1 @20
  SAY BATTLE_CRY2 @21
  SAY BATTLE_CRY3 @20
  SAY BATTLE_CRY4 @22
  SAY BATTLE_CRY5 @21
  SAY DAMAGE ~~ [UDSOLA05]
  SAY DYING ~~ [UDSOLA06]
  SAY HURT @22
  SAY AREA_FOREST ~~ []
  SAY AREA_CITY ~~ []
  SAY AREA_DUNGEON @30
  SAY AREA_DAY ~~ []
  SAY AREA_NIGHT ~~ []
  SAY SELECT_COMMON1 @24
  SAY SELECT_COMMON2 @17
  SAY SELECT_COMMON3 @15
  SAY SELECT_COMMON4 @24
  SAY SELECT_COMMON5 @26
  SAY SELECT_COMMON6 @25
  SAY SELECT_ACTION1 @27
  SAY SELECT_ACTION2 @17
  SAY SELECT_ACTION3 @26
  SAY SELECT_ACTION4 @23
  SAY SELECT_ACTION5 @24
  SAY SELECT_ACTION6 @12
  SAY SELECT_ACTION7 @25
//  SAY SELECT_RARE1 ~~ []
//  SAY SELECT_RARE2 ~~ []
  SAY CRITICAL_HIT @28
  SAY CRITICAL_MISS @29
  SAY TARGET_IMMUNE @29
  SAY INVENTORY_FULL ~~ []
  SAY PICKED_POCKET ~~
  SAY HIDDEN_IN_SHADOWS ~~
  SAY SPELL_DISRUPTED @29
  SAY SET_A_TRAP ~~
SAY BIO @10


COPY ~C#Solaufein/Creatures/C#SOcre1.cre~ ~override/C#SOcre1.CRE~  //Imrae
COPY ~C#Solaufein/Creatures/c#sodrf0.cre~ ~override/c#sodrf0.CRE~ 
WRITE_ASCII 0x248 ~c#sodrsc~ #8  //override script
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrf0~ #8
WRITE_ASCII 0x2cc ~c#sodrf0~ #8 
COPY ~C#Solaufein/Creatures/c#sodrf0.cre~ ~override/c#sodrf1.CRE~
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrf1~ #8
WRITE_ASCII 0x2cc ~c#sodrf1~ #8 
COPY ~C#Solaufein/Creatures/c#sodrm0.cre~ ~override/c#sodrm0.CRE~ 
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrm0~ #8
WRITE_ASCII 0x2cc ~c#sodrm0~ #8 
COPY ~C#Solaufein/Creatures/c#sodrm0.cre~ ~override/c#sodrm1.CRE~
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrm1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrm1~ #8
WRITE_ASCII 0x2cc ~c#sodrm1~ #8 
COPY ~C#Solaufein/Creatures/c#sodrc0.cre~ ~override/c#sodrc0.CRE~ 
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~C#solau1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrc0~ #8
WRITE_ASCII 0x2cc ~c#sodrc0~ #8 

/* Driders. Polymorph to drider-animation via script in-game! */

/* male */
COPY ~C#Solaufein/Creatures/c#sodrf0.cre~ ~override/c#sodrd1.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd1~ #8
WRITE_ASCII 0x2cc ~c#sodrd1~ #8 

COPY ~C#Solaufein/Creatures/c#sodrf0.cre~ ~override/c#sodrd2.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrm1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd2~ #8
WRITE_ASCII 0x2cc ~c#sodrd2~ #8 

COPY ~C#Solaufein/Creatures/c#sodrm0.cre~ ~override/c#sodrd3.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd3~ #8
WRITE_ASCII 0x2cc ~c#sodrd3~ #8 

/* female */
COPY ~C#Solaufein/Creatures/c#sodrc0.cre~ ~override/c#sodrd4.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrf0~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd4~ #8
WRITE_ASCII 0x2cc ~c#sodrd4~ #8 

COPY ~C#Solaufein/Creatures/c#sodrf0.cre~ ~override/c#sodrd5.CRE~
//SAY NAME1 @11
//SAY NAME2 @11
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x250 ~c#sodrm1~ #8  //class script
WRITE_ASCII 0x280 ~c#sodrd5~ #8
WRITE_ASCII 0x2cc ~c#sodrd5~ #8 

/* spiders */

COPY ~C#Solaufein/Creatures/c#sospd1.cre~ ~override/c#sospd1.CRE~
WRITE_ASCII 0x248 ~c#sodrsc~ #8
WRITE_ASCII 0x280 ~c#sospd1~ #8
WRITE_ASCII 0x2cc ~c#sospd1~ #8 


/* dead spiders */

COPY ~C#Solaufein/Creatures/c#sospdd.cre~ ~override/c#sospdd.CRE~

/* SvA quest */
COPY ~C#Solaufein/Creatures/c#somob1.cre~ ~override/c#somob1.CRE~
WRITE_ASCII 0x248 ~c#somob1~ #8  //override script
WRITE_ASCII 0x280 ~c#somob1~ #8
WRITE_ASCII 0x2cc ~c#somob1~ #8 

COPY ~C#Solaufein/Creatures/c#somob2.cre~ ~override/c#somob2.CRE~
SAY NAME1 @31
SAY NAME2 @31
WRITE_ASCII 0x248 ~c#somob2~ #8  //override script
WRITE_ASCII 0x280 ~c#somob2~ #8
WRITE_ASCII 0x2cc ~c#somob2~ #8 

COPY ~C#Solaufein/Creatures/c#somob3.cre~ ~override/c#somob3.CRE~
SAY NAME1 @32
SAY NAME2 @32
WRITE_ASCII 0x248 ~c#somob3~ #8  //override script
WRITE_ASCII 0x280 ~c#somob3~ #8
WRITE_ASCII 0x2cc ~c#somob3~ #8 

COPY ~C#Solaufein/Creatures/c#somob4.cre~ ~override/c#somob4.CRE~
SAY NAME1 @33
SAY NAME2 @33
WRITE_ASCII 0x248 ~c#somob4~ #8  //override script
WRITE_ASCII 0x280 ~c#somob4~ #8
WRITE_ASCII 0x2cc ~c#somob4~ #8 

COPY ~C#Solaufein/Creatures/c#somob5.cre~ ~override/c#somob5.CRE~
SAY NAME1 @34
SAY NAME2 @34
WRITE_ASCII 0x248 ~c#somob5~ #8  //override script
WRITE_ASCII 0x280 ~c#somob5~ #8
WRITE_ASCII 0x2cc ~c#somob5~ #8 



//Dialoguesets

//Dialoguesets für installiertes ToB
APPEND ~pdialog.2da~
~C#Solaufein 	C#SolauP 	C#SolauJ 	C#SolauD 	C#SolauP 	C#SolauJ 		C#SolauD 	C#Solau~
UNLESS ~C#Solaufein~


APPEND ~interdia.2da~
~C#Solaufein 	C#SolauB      C#SolauB~
UNLESS ~C#Solaufein~

//Damit Imoen auch was sagen darf [taken from BGII fixpack at G3]
ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
  COPY_EXISTING ~interdia.2da~ ~override~
    SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
    BUT_ONLY_IF_IT_CHANGES
END ELSE BEGIN
  APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
    UNLESS ~BIMOEN2~
END


ACTION_IF GAME_IS ~eet~ BEGIN
  INCLUDE ~EET/other/EET_functions.tph~
  LAF ~EET_NPC_TRANSITION~
    INT_VAR
      type = 2
      default_ToB = 1
    STR_VAR
      dv = "C#Solaufein"
      override_SoA = "C#SOLAU"
      override_ToB = "C#SOLAU"
      dialog_ToB = "C#SOLA25"
      cre_ToB = "C#Soltob"
      traFile = EVAL "C#Solaufein/Translations/autotra/%LANGUAGE%/c#solatra.tra"
      string = "@1328" //Bring me Solaufein, the drow fighter, who I saved from the drider cocoons.
      stringPosDV = "Tiax"
  END
END ELSE BEGIN
  COMPILE ~c#solaufein/Dialogues/C#SOLAU_ToB_fatesp.D~ USING ~C#Solaufein/Translations/autotra/%LANGUAGE%/c#solatra.tra~
END


/* Ensures that Solaufein won't leave the party because of reputation */

COPY_EXISTING ~dplayer2.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY ~BreakingPoint()~
      ~BreakingPoint()
       !Name("C#Solaufein",Myself)~

    REPLACE_TEXTUALLY ~!Name("baeloth",Myself)~
      ~!Name("baeloth",Myself) 
       !Name("C#Solaufein",Myself)~
  COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES





/* Drider animations: BGII:EE (female/male) 
taken out of viader's Extended Animations Mod with many thanks! */

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN

COPY ~C#Solaufein/animations/ee/sound~ ~override~
COPY ~C#Solaufein/animations/ee/bam~ ~override~

EXTEND_BOTTOM ~C#SO01.bcs~ ~c#solaufein/scripts/C#So01_driderchange.baf~ EVALUATE_BUFFER

COPY_EXISTING ~C#sococ2.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1251\.844\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ3.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd5",\[1456\.1083\],[0S])\)~
~\1 ActionOverride("c#sodrd5",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd5",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ5.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1592\.1241\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES


END //~bg2ee eet~

/* crossmod */


/* Eilistraee's Song */
ACTION_IF FILE_EXISTS_IN_GAME ~ESxMH.dlg~ BEGIN
  PRINT @90010


INCLUDE ~C#Solaufein/lib/extra_regexp_vars.tph~
OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~

COPY_EXISTING ~AR2401.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(!Exists("C#Solaufein")[%newline%]*!InParty("C#Solaufein")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

// Get state for C#SOLAU %dazu_04% @156 (c#solau.d)
/* ~(snort) So much for my noble approach. But I understand your answer - I would have done the same, in your place. I will follow you discretely then.~ */
OUTER_SET dazu_04 = STATE_WHICH_SAYS 156 IN ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ FROM ~c#solau~

// Get state for C#SOLAU %dazu_05% @2703 (c#solau.d)
/* ~Search for me outside of the elven camp.~ */
OUTER_SET dazu_05 = STATE_WHICH_SAYS 2703 IN ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ FROM ~c#solau~

COMPILE EVALUATE_BUFFER ~C#Solaufein/crossmod/eilistraeessong_compatibility.d~

/* my Solaufein will talk about Eilistraee's Clearing if he joins at Underdark exit */
EXTEND_BOTTOM ~C#SOLAU.bcs~   ~C#Solaufein/crossmod/Eilistraeessong_c#solau.baf~ EVALUATE_BUFFER

/* remove extra Solaufein from ES (my Solaufein will take over quest start) */
EXTEND_TOP ~AR2401.bcs~ ~C#Solaufein/crossmod/eilistraeessong_ar2401.baf~

END //ES

/*
/////////////////////////////////////
 Give Ust Natha Solaufein the Portrait 
/////////////////////////////////////
*/

BEGIN @90001
DESIGNATED 1
LABEL JasteysSolaufeinRescue-Give_Ust_Natha_Solaufein_Portrait

REQUIRE_COMPONENT ~c#solaufein/setup-c#solaufein.tp2~ ~0~ @90003

/* Give Solaufein in Ust Natha the portrait! */

COPY_EXISTING ~UDSOLA01.CRE~ ~override~
	~UDSOLA02.CRE~ ~override~
WRITE_ASCII 0x34 ~C#SOLAUS~
WRITE_ASCII 0x3c ~C#SOLAUM~
BUT_ONLY_IF_IT_CHANGES






/*
/////////////////////////////////////
 Drider animations 
/////////////////////////////////////

 */

BEGIN @90002
DESIGNATED 2
LABEL JasteysSolaufeinRescue-Drider_Animations

REQUIRE_PREDICATE !(GAME_IS ~bg2ee eet~) @90004
/* BGII / BGT version: IA needs to be installed for the drider animations */
REQUIRE_PREDICATE ((GAME_IS ~bg2 tob bgt~) AND (MOD_IS_INSTALLED ~SETUP-INFINITYANIMATIONS.TP2~ "0")) @90005 

REQUIRE_COMPONENT ~c#solaufein/setup-c#solaufein.tp2~ ~0~ @90003


COPY ~c#solaufein/animations/bg2~ ~override~


EXTEND_BOTTOM ~C#SO01.bcs~ ~c#solaufein/scripts/C#So01_driderchange.baf~ EVALUATE_BUFFER

COPY_EXISTING ~C#sococ2.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1251\.844\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ3.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd5",\[1456\.1083\],[0S])\)~
~\1 ActionOverride("c#sodrd5",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd5",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~C#sococ5.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
REPLACE_TEXTUALLY ~\(CreateCreature("c#sodrd2",\[1592\.1241\],[0S])\)~
~\1 ActionOverride("c#sodrd2",Polymorph(%Drideranimation_male%)) ActionOverride("c#sodrd2",SetName(@11))~
END
BUT_ONLY_IF_IT_CHANGES



//////////////////////////////////////////////////////////////////
// Install alternative portrait #2 from Chinasky //
/////////////////////////////////////////////////////////

BEGIN @90006
DESIGNATED 3
LABEL JasteysSolaufeinRescue-Alternative_Portrait
REQUIRE_COMPONENT ~c#solaufein/setup-c#solaufein.tp2~ ~0~ @90003


ACTION_IF GAME_IS ~bg2 tob bgt~ BEGIN

COPY ~C#Solaufein/Portraits/Portrait2/C#SolauL.bmp~ ~override/C#SolauL.bmp~
COPY ~C#Solaufein/Portraits/Portrait2/C#SolauM.bmp~ ~override/C#SolauM.bmp~
COPY ~C#Solaufein/Portraits/Portrait2/C#SolauS.bmp~ ~override/C#SolauS.bmp~

END

ACTION_IF GAME_IS ~bg2ee eet~ BEGIN

COPY ~C#Solaufein/Portraits/Portrait2/C#SolauL.bmp~ ~override/C#SolauL.bmp~
COPY ~C#Solaufein/Portraits/Portrait2/Portrait2_EE/C#SolauM.bmp~ ~override/C#SolauM.bmp~
COPY ~C#Solaufein/Portraits/Portrait2/Portrait2_EE/C#SolauM.bmp~ ~override/C#SolauS.bmp~

END

//////////////////////////////////////////////////////////////////
// Install Detection for Drow PC //
/////////////////////////////////////////////////////////

BEGIN @90007
DESIGNATED 4
LABEL JasteysSolaufeinRescue-Detection_for_Drow_PC
REQUIRE_COMPONENT ~c#solaufein/setup-c#solaufein.tp2~ ~0~ @90003

/* detect subrace mods */
ACTION_IF (FILE_EXISTS_IN_GAME ~SED.spl~ OR (FILE_EXISTS_IN_GAME ~SrED.spl~ AND NOT FILE_EXISTS_IN_GAME ~srsquir.cre~)) BEGIN
  PRINT @90008
  EXTEND_TOP ~AR2100.bcs~ ~C#Solaufein/crossmod/AR2100_subraceBGII.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
END

/* subrace mod from Corsymyr. EE only */
ACTION_IF (FILE_EXISTS_IN_GAME ~SrED.spl~ AND FILE_EXISTS_IN_GAME ~srsquir.cre~) BEGIN
  PRINT @90009
  EXTEND_TOP ~AR2100.bcs~ ~C#Solaufein/crossmod/AR2100_subraceBGEE_Corsymyr.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
END

/* subrace mod from tipun. EE only */
/* TriggerOverride(Player1,Global("SUBRACE","LOCALS",3)) */
ACTION_IF (FILE_EXISTS_IN_GAME ~SR_DROW.spl~) BEGIN
  PRINT @90014
  EXTEND_TOP ~AR2100.bcs~ ~C#Solaufein/crossmod/AR2100_subraceBGEE_tipun.baf~
USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
END

ACTION_IF (!FILE_EXISTS_IN_GAME ~SED.spl~ AND !FILE_EXISTS_IN_GAME ~SrED.spl~ AND !FILE_EXISTS_IN_GAME ~SR_DROW.spl~) THEN BEGIN
  PRINT @90013 /* No subrace mod detected. Installing drow specific reply option to Adalon's dialogue...~ */
  COMPILE EVALUATE_BUFFER ~C#Solaufein/Dialogues/detect_drowpc.d~
	USING ~C#Solaufein/Translations/autotra/%s/c#solatra.tra~ 
END
